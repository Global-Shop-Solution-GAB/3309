Program.Sub.ScreenSU.Start
gui.FrmInfo..create
gui.FrmInfo..caption("Financial Statement Account Information")
gui.FrmInfo..size(11100,9180)
gui.FrmInfo..minx(10200)
gui.FrmInfo..miny(9200)
gui.FrmInfo..position(0,0)
gui.FrmInfo..event(unload,Unload)
gui.FrmInfo..alwaysontop(False)
gui.FrmInfo..fontname("Arial")
gui.FrmInfo..fontsize(8)
gui.FrmInfo..forecolor(0)
gui.FrmInfo..fontstyle(,,,,)
gui.FrmInfo..BackColor(-2147483633)
gui.FrmInfo..controlbox(True)
gui.FrmInfo..maxbutton(False)
gui.FrmInfo..minbutton(True)
gui.FrmInfo..mousepointer(0)
gui.FrmInfo..moveable(True)
gui.FrmInfo..sizeable(False)
gui.FrmInfo..ShowInTaskBar(True)
gui.FrmInfo..titlebar(True)
gui.FrmInfo.gsfgInfo.create(gsflexgrid)
gui.FrmInfo.gsfgInfo.FixedRows(0)
gui.FrmInfo.gsfgInfo.FixedCols(0)
gui.FrmInfo.gsfgInfo.visible(True)
gui.FrmInfo.gsfgInfo.size(10510,7730)
gui.FrmInfo.gsfgInfo.zorder(0)
gui.FrmInfo.gsfgInfo.position(200,200)
gui.FrmInfo.gsfgInfo.enabled(True)
gui.FrmInfo.gsfgInfo.event(mouseup,gsfgLayout_mouseup)
gui.FrmInfo.cmdSave.create(button)
gui.FrmInfo.cmdSave.caption("Save")
gui.FrmInfo.cmdSave.visible(True)
gui.FrmInfo.cmdSave.size(855,375)
gui.FrmInfo.cmdSave.zorder(0)
gui.FrmInfo.cmdSave.position(300,8100)
gui.FrmInfo.cmdSave.enabled(True)
gui.FrmInfo.cmdSave.fontname("Arial")
gui.FrmInfo.cmdSave.fontsize(8)
gui.FrmInfo.cmdSave.event(click,cmdSave_click)
gui.FrmInfo.cmdSave.defaultvalue("")
gui.FrmInfo.cmdSave.controlgroup(0)
gui.FrmInfo.cmdCancel.create(button)
gui.FrmInfo.cmdCancel.caption("Cancel")
gui.FrmInfo.cmdCancel.visible(False)
gui.FrmInfo.cmdCancel.size(855,375)
gui.FrmInfo.cmdCancel.zorder(0)
gui.FrmInfo.cmdCancel.position(1300,8100)
gui.FrmInfo.cmdCancel.enabled(False)
gui.FrmInfo.cmdCancel.fontname("Arial")
gui.FrmInfo.cmdCancel.fontsize(8)
gui.FrmInfo.cmdCancel.event(click,Unload)
gui.FrmInfo.cmdCancel.defaultvalue("")
gui.FrmInfo.cmdCancel.controlgroup(0)
gui.FrmInfo.progress1.create(progressbar)
gui.FrmInfo.progress1.visible(False)
gui.FrmInfo.progress1.size(5000,400)
gui.FrmInfo.progress1.zorder(0)
gui.FrmInfo.progress1.position(3000,3700)
gui.FrmInfo.progress1.enabled(True)
gui.FrmInfo.cmdLayout.create(button)
gui.FrmInfo.cmdLayout.caption("Layout")
gui.FrmInfo.cmdLayout.visible(False)
gui.FrmInfo.cmdLayout.size(855,375)
gui.FrmInfo.cmdLayout.zorder(0)
gui.FrmInfo.cmdLayout.position(2300,8100)
gui.FrmInfo.cmdLayout.enabled(False)
gui.FrmInfo.cmdLayout.fontname("Arial")
gui.FrmInfo.cmdLayout.fontsize(8)
gui.FrmInfo.cmdLayout.event(click,cmdLayout_click)
gui.FrmInfo.cmdLayout.defaultvalue("")
gui.FrmInfo.cmdLayout.controlgroup(0)
gui.FrmInfo.lblFlagY.create(label,"Y - Add Acct #'s",True,1500,200,1,8500,8000,True,0,Arial,8,-2147483633,0)
gui.FrmInfo.lblFlagY.defaultvalue("")
gui.FrmInfo.lblFlagY.controlgroup(0)
gui.FrmInfo.lblFlagA.create(label,"A - Add Group Order #'s",True,2475,200,1,8500,8200,True,0,Arial,8,-2147483633,0)
gui.FrmInfo.lblFlagA.defaultvalue("")
gui.FrmInfo.lblFlagA.controlgroup(0)
gui.FrmInfo.lblFlagS.create(label,"S - Subtract Group Order #'s",True,2475,200,1,8500,8400,True,0,Arial,8,-2147483633,0)
gui.FrmInfo.lblFlagS.defaultvalue("")
gui.FrmInfo.lblFlagS.controlgroup(0)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Variable.UDT.GAB_FIN_STATE_INFO.Define("ID",Long,ID)
Variable.UDT.GAB_FIN_STATE_INFO.Define("STATEMENT_NUM",Long,STATEMENT_NUM)
Variable.UDT.GAB_FIN_STATE_INFO.Define("GROUP_ORDER",Long,GROUP_ORDER)
Variable.UDT.GAB_FIN_STATE_INFO.Define("SUB_GROUP",Long,SUB_GROUP)
Variable.UDT.GAB_FIN_STATE_INFO.Define("BEG_ACCOUNT",String,BEG_ACCOUNT)
Variable.UDT.GAB_FIN_STATE_INFO.Define("END_ACCOUNT",String,END_ACCOUNT)
Variable.UDT.GAB_FIN_STATE_INFO.Define("FLAG_TOTAL",String,FLAG_TOTAL)
Variable.UDT.GAB_FIN_STATE_INFO.Define("TITLE",String,TITLE)
Variable.uGlobal.uAcctInfo.Declare("GAB_FIN_STATE_INFO")
Program.Sub.Preflight.End

Program.Sub.Main.Start
'This is going to be used to create the layout of the report for Financial Statements
F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.sIcon.Declare(String)

'F.Intrinsic.String.Build("{0}\gss.ico",V.Caller.InstallDir,V.Local.sIcon)
'Gui.FrmInfo..Icon(V.Local.sIcon)

F.ODBC.Connection!con.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.Control.CallSub(Subcreatetable)
F.Intrinsic.Control.CallSub(Subbuildstyles)
F.Intrinsic.Control.CallSub(Subpopulateudts)

Gui.FrmInfo..Show

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.Main.End

Program.Sub.subBuildStyles.Start
'Define Build Styles for Flex Grid
F.Intrinsic.Control.SetErrorHandler("subBuildStyles_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

'Set Column
Gui.FrmInfo.gsfgInfo.Cols(10)

'Set Rows
Gui.FrmInfo.gsfgInfo.Rows(2)
Gui.FrmInfo.gsfgInfo.FixedRows(1)
Gui.FrmInfo.gsfgInfo.RowHeight(0,500)

Gui.FrmInfo.gsfgInfo.SetProperty("Enter",2)

'ID
Gui.FrmInfo.gsfgInfo.TextMatrix(0,0,"ID")
Gui.FrmInfo.gsfgInfo.ColWidth(0,500)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,0,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,0,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,0,"Type","Locked","True")
Gui.FrmInfo.gsfgInfo.BuildStyle(1,0,"Color","Back",-2147483633)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,0,"Format","Numeric",True)

'Statement Number
V.Local.sStatementNumber.Declare(String)
F.Intrinsic.String.Build("Statement{0}Number",V.Ambient.NewLine,V.Local.sStatementNumber)
Gui.FrmInfo.gsfgInfo.TextMatrix(1,0,V.Local.sStatementNumber)
Gui.FrmInfo.gsfgInfo.ColWidth(1,1000)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,1,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,1,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,1,"Format","Numeric",True)

'Group Order
V.Local.sGroupOrder.Declare(String)
F.Intrinsic.String.Build("Group{0}Order",V.Ambient.NewLine,V.Local.sGroupOrder)
Gui.FrmInfo.gsfgInfo.TextMatrix(2,0,V.Local.sGroupOrder)
Gui.FrmInfo.gsfgInfo.ColWidth(2,800)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,2,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,2,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,2,"Format","Numeric",True)

'Subgroup
Gui.FrmInfo.gsfgInfo.TextMatrix(3,0,"Subgroup")
Gui.FrmInfo.gsfgInfo.ColWidth(3,800)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,3,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,3,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,3,"Format","Numeric",True)

'Title
Gui.FrmInfo.gsfgInfo.TextMatrix(4,0,"Title")
Gui.FrmInfo.gsfgInfo.ColWidth(4,3000)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,4,"Format","Alignment",2)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,4,"Format","Alignment",2)

'Beginning Account
V.Local.sBegAcct.Declare(String)
F.Intrinsic.String.Build("Beginning{0}Account",V.Ambient.NewLine,V.Local.sBegAcct)
Gui.FrmInfo.gsfgInfo.TextMatrix(5,0,V.Local.sBegAcct)
Gui.FrmInfo.gsfgInfo.ColWidth(5,1200)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,5,"Format","Alignment",2)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,5,"Format","Alignment",2)

'Ending Account
V.Local.sEndAcct.Declare(String)
F.Intrinsic.String.Build("Ending{0}Account",V.Ambient.NewLine,V.Local.sEndAcct)
Gui.FrmInfo.gsfgInfo.TextMatrix(6,0,V.Local.sEndAcct)
Gui.FrmInfo.gsfgInfo.ColWidth(6,1200)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,6,"Format","Alignment",2)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,6,"Format","Alignment",2)

'Flags
V.Local.sFlagTot.Declare(String)
Gui.FrmInfo.gsfgInfo.TextMatrix(7,0,"Flags")
Gui.FrmInfo.gsfgInfo.ColWidth(7,500)
Gui.FrmInfo.gsfgInfo.BuildStyle(0,7,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,7,"Format","Alignment",4)

'Add Row
Gui.FrmInfo.gsfgInfo.TextMatrix(8,0,"")
Gui.FrmInfo.gsfgInfo.ColWidth(8,600)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,8,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,8,"Type","Command","Add")

'Delete Row
Gui.FrmInfo.gsfgInfo.TextMatrix(9,0,"")
Gui.FrmInfo.gsfgInfo.ColWidth(9,600)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,9,"Format","Alignment",4)
Gui.FrmInfo.gsfgInfo.BuildStyle(1,9,"Type","Command","Delete")


'Apply Styles to the grid
Gui.FrmInfo.gsfgInfo.ApplyStyle(0,0)
Gui.FrmInfo.gsfgInfo.ApplyStyle(1,1)

F.Intrinsic.Control.ExitSub

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subBuildStyles_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.subBuildStyles.End

Program.Sub.Unload.Start
F.Intrinsic.Control.SetErrorHandler("Unload_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

F.ODBC.Connection!con.Close
F.Intrinsic.Control.End

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("Unload_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.Unload.End

Program.Sub.subPopulateUDTs.Start
'Populate the flexgrid with data if populated
F.Intrinsic.Control.SetErrorHandler("subPopulateUDTs_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.sSQL.Declare(String)
V.Local.sRet.Declare(String)
V.Local.iRet.Declare(Long)
V.Local.bFileExists.Declare(Boolean)
V.Local.sCopyFrom.Declare(String)
V.Local.sCopyTo.Declare(String)

F.Intrinsic.String.Build("{0}\GAB_FIN_STATE_INFO.MKD",V.Caller.DDFDir,V.Local.sCopyFrom)
F.Intrinsic.String.Build("{0}\GAB_FIN_STATE_INFO_BEFORE.MKD",V.Caller.DDFDir,V.Local.sCopyTo)

F.ODBC.Connection!con.OpenRecordsetRO("rstInfo","SELECT * FROM GAB_FIN_STATE_INFO ORDER BY ID;")
F.Intrinsic.Control.If(V.ODBC.con!rstInfo.EOF,=,False)
	F.Intrinsic.Variable.LoadUDTFromRecordset("con","rstInfo","V.uGlobal.uAcctInfo",False,100000)
	F.Intrinsic.Control.CallSub(Subloadgsfg)
	F.ODBC.con!rstInfo.Close
F.Intrinsic.Control.Else
	F.ODBC.con!rstInfo.Close
	F.Intrinsic.File.Exists(V.Local.sCopyTo,V.Local.bFileExists)
	F.Intrinsic.Control.If(V.Local.bFileExists,=,True)
		F.Intrinsic.UI.Msgbox("Do you want to restore the orignal file?","Restore",4,V.Local.iRet)
		F.Intrinsic.Control.If(V.Local.iRet,=,6)
			F.Intrinsic.File.DeleteFile(V.Local.sCopyFrom)
			F.Intrinsic.File.CopyFile(V.Local.sCopyTo,V.Local.sCopyFrom)
			F.Intrinsic.UI.Msgbox("Please relaunch the program to populate the restored data.","Restore Complete")
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.Else
			Gui.FrmInfo.gsfgInfo.TextMatrix(0,1,1)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Else
		Gui.FrmInfo.gsfgInfo.TextMatrix(0,1,1)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subPopulateUDTs_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.subPopulateUDTs.End

Program.Sub.subLoadGSFG.Start
'Load the GSFG from an UDT

F.Intrinsic.Control.SetErrorHandler("subLoadGSFG_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.bLoadGSFG.Declare(Boolean,True)

Gui.FrmInfo.gsfgInfo.LoadFromUDT("V.uGlobal.uAcctInfo","ID::0*!*Statement_Num::1*!*Group_Order::2*!*Sub_Group::3*!*Title::4*!*Beg_Account::5*!*End_Account::6*!*Flag_Total::7",2)
F.Intrinsic.Control.CallSub(Subapplystyles,"LoadGSFG",V.Local.bLoadGSFG)

F.Intrinsic.Control.ExitSub

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subLoadGSFG_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf



Program.Sub.subLoadGSFG.End

Program.Sub.subApplyStyles.Start
'Set Styles for each row

F.Intrinsic.Control.SetErrorHandler("subApplyStyles_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.iC.Declare(Long,1)
V.Local.iRows.Declare(Long)
V.Local.iNewRow.Declare(Long)

F.Intrinsic.Control.If(V.Args.LoadGSFG,=,True)
	F.Intrinsic.Math.Add(V.Screen.FrmInfo!gsfgInfo.Rows,1,V.Local.iNewRow)
	Gui.FrmInfo.gsfgInfo.Rows(V.Local.iNewRow)
F.Intrinsic.Control.EndIf

F.Intrinsic.Math.Sub(V.Screen.FrmInfo!gsfgInfo.Rows,1,V.Local.iRows)
F.Intrinsic.Control.For(V.Local.iC,1,V.Local.iRows,1)
	Gui.FrmInfo.gsfgInfo.ApplyStyle(V.Local.iC,1)
	Gui.FrmInfo.gsfgInfo.TextMatrix(0,V.Local.iC,V.Local.iC)
F.Intrinsic.Control.Next(V.Local.iC)

F.Intrinsic.Control.ExitSub

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subApplyStyles_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.subApplyStyles.End

Program.Sub.cmdSave_click.Start
F.Intrinsic.Control.SetErrorHandler("cmdsave_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.iCnt.Declare(Long,1)
V.Local.iC.Declare(Long,-1)
V.Local.iID.Declare(Long)
V.Local.iStateNum.Declare(Long)
V.Local.sStateName.Declare(String)
V.Local.iGrpOrd.Declare(Long)
V.Local.iSubgroup.Declare(String)
V.Local.sTitle.Declare(String)
V.Local.sBegAcct.Declare(String)
V.Local.sEndAcct.Declare(String)
V.Local.sTotFlg.Declare(String)
V.Local.iRowTotal.Declare(Long)
V.Local.iProgressBar.Declare(Long)
V.Local.sCopyFrom.Declare(String)
V.Local.sCopyTo.Declare(String)

Gui.FrmInfo.gsfgInfo.Visible(False)
Gui.FrmInfo.progress1.Visible(True)


F.Intrinsic.String.Build("{0}\GAB_FIN_STATE_INFO.MKD",V.Caller.DDFDir,V.Local.sCopyFrom)
F.Intrinsic.String.Build("{0}\GAB_FIN_STATE_INFO_BEFORE.MKD",V.Caller.DDFDir,V.Local.sCopyTo)
F.Intrinsic.File.CopyOpenFile(V.Local.sCopyFrom,V.Local.sCopyTo)

V.uGlobal.uAcctInfo.Redim(-1,-1)
F.ODBC.Connection!con.Execute("DELETE FROM GAB_FIN_STATE_INFO;")

F.Intrinsic.Math.Sub(V.Screen.FrmInfo!gsfgInfo.Rows,1,V.Local.iRowTotal)
Gui.FrmInfo.progress1.Max(V.Local.iRowTotal)
F.Intrinsic.Control.For(V.Local.iCnt,1,V.Local.iRowTotal,1)
	Gui.FrmInfo.progress1.Value(V.Local.iCnt)
	F.Intrinsic.Math.Add(V.Local.iC,1,V.Local.iC)
	F.Intrinsic.Control.If(V.Local.iC,=,0)
		V.uGlobal.uAcctInfo.Redim(0,0)
	F.Intrinsic.Control.Else
		V.uGlobal.uAcctInfo.RedimPreserve(0,V.Local.iC)
	F.Intrinsic.Control.EndIf
'ID
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(0,V.Local.iCnt,V.Local.iID)
	V.uGlobal.uAcctInfo(V.Local.iC)!ID.Set(V.Local.iID.Long)
'Statement Num
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(1,V.Local.iCnt,V.Local.iStateNum)
	V.uGlobal.uAcctInfo(V.Local.iC)!STATEMENT_NUM.Set(V.Local.iStateNum.Long)
'Group Order
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(2,V.Local.iCnt,V.Local.iGrpOrd)
	V.uGlobal.uAcctInfo(V.Local.iC)!GROUP_ORDER.Set(V.Local.iGrpOrd.Long)
'Subgroup
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(3,V.Local.iCnt,V.Local.iSubgroup)
	V.uGlobal.uAcctInfo(V.Local.iC)!SUB_GROUP.Set(V.Local.iSubgroup.Long)
'Title
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(4,V.Local.iCnt,V.Local.sTitle)
	F.Intrinsic.Control.If(V.Local.iSubgroup,=,0)
		V.uGlobal.uAcctInfo(V.Local.iC)!TITLE.Set(V.Local.sTitle.UCase)
	F.Intrinsic.Control.Else
		V.uGlobal.uAcctInfo(V.Local.iC)!TITLE.Set(V.Local.sTitle.String)
	F.Intrinsic.Control.EndIf
'Beginning Accoubt
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(5,V.Local.iCnt,V.Local.sBegAcct)
	V.uGlobal.uAcctInfo(V.Local.iC)!BEG_ACCOUNT.Set(V.Local.sBegAcct.String)
'Ending Account
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(6,V.Local.iCnt,V.Local.sEndAcct)
	V.uGlobal.uAcctInfo(V.Local.iC)!END_ACCOUNT.Set(V.Local.sEndAcct.String)
'Total Flag
	Gui.FrmInfo.gsfgInfo.GetTextMatrix(7,V.Local.iCnt,V.Local.sTotFlg)
	F.Intrinsic.Control.If(V.Local.sTotFlg,=,"")
		V.uGlobal.uAcctInfo(V.Local.iC)!FLAG_TOTAL.Set("Y")
	F.Intrinsic.Control.Else
		V.uGlobal.uAcctInfo(V.Local.iC)!FLAG_TOTAL.Set(V.Local.sTotFlg.UCase)
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Next(V.Local.iCnt)

F.ODBC.Connection!con.OpenRecordsetRW("rstSave","SELECT * FROM GAB_FIN_STATE_INFO ORDER BY ID;")
F.Intrinsic.Variable.SaveUDTToRecordset("con","rstSave","V.uGlobal.uAcctInfo")
F.ODBC.con!rstSave.Close

F.ODBC.Connection!con.Execute("DELETE FROM GAB_FIN_STATE_INFO WHERE STATEMENT_NUM = '0' AND GROUP_ORDER = '0' AND SUB_GROUP = '0' AND TITLE = '';")
F.Intrinsic.Control.CallSub(Subpopulateudts)

'F.Intrinsic.UI.Msgbox("Data Saved.","Complete")
Gui.FrmInfo.gsfgInfo.Visible(True)
Gui.FrmInfo.progress1.Visible(False)

F.Intrinsic.Control.ExitSub

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("cmdsave_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Layout.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.cmdSave_click.End

Program.Sub.subRowAdd.Start
F.Intrinsic.Control.SetErrorHandler("subRowAdd_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.iInsertRow.Declare(Long)
V.Local.bLoadGSFG.Declare(Boolean,False)

Gui.FrmInfo.gsfgInfo.Visible(False)

F.Intrinsic.Math.Add(V.Args.Row,1,V.Local.iInsertRow)

Gui.FrmInfo.gsfgInfo.InsertRow(V.Local.iInsertRow)

F.Intrinsic.Control.CallSub(Subapplystyles,"LoadGSFG",V.Local.bLoadGSFG)

Gui.FrmInfo.gsfgInfo.Visible(True)

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subRowAdd_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf



Program.Sub.subRowAdd.End

Program.Sub.subRowDel.Start
F.Intrinsic.Control.SetErrorHandler("subRowDel_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.bLoadGSFG.Declare(Boolean,False)

Gui.FrmInfo.gsfgInfo.Visible(False)

F.Intrinsic.Control.If(V.Screen.FrmInfo!gsfgInfo.Rows,>,2)
	Gui.FrmInfo.gsfgInfo.DeleteRow(V.Args.Row)
F.Intrinsic.Control.Else
	F.Intrinsic.UI.Msgbox("This is the last row and you cannot remove it. Please edit the data if needed and save.","Row Check")
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.CallSub(Subapplystyles,"LoadGSFG",V.Local.bLoadGSFG)

Gui.FrmInfo.gsfgInfo.Visible(True)

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subRowDel_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Acct_Info.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.subRowDel.End

Program.Sub.gsfgLayout_mouseup.Start
F.Intrinsic.Control.SetErrorHandler("gsfgLayout_mouseup_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.iRow.Declare(Long)
V.Local.iCol.Declare(Long)

V.Local.iRow.Set(V.Screen.FrmInfo!gsfgInfo.Row)
V.Local.iCol.Set(V.Screen.FrmInfo!gsfgInfo.Col)

'F.Intrinsic.UI.Msgbox(V.Args.MouseRow,"MouseRow")
'F.Intrinsic.UI.Msgbox(V.Local.iRow,"Row")
'F.Intrinsic.UI.Msgbox(V.Local.iCol,"Col")

F.Intrinsic.Control.If(V.Args.MouseRow.Long,=,0)
	F.Intrinsic.Control.AndIf(V.Local.iRow,=,1)
	F.Intrinsic.UI.Sleep(1)
F.Intrinsic.Control.Else
	F.Intrinsic.Control.SelectCase(V.Local.iCol)
		F.Intrinsic.Control.Case(8)
			F.Intrinsic.Control.CallSub(Subrowadd,"Row",V.Local.iRow,"Col",V.Local.iCol)
		F.Intrinsic.Control.Case(9)
			F.Intrinsic.Control.CallSub(Subrowdel,"Row",V.Local.iRow,"Col",V.Local.iCol)
	F.Intrinsic.Control.EndSelect
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.ExitSub

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("gsfgLayout_mouseup_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Layout.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf



Program.Sub.gsfgLayout_mouseup.End

Program.Sub.cmdLayout_click.Start
F.Intrinsic.Control.SetErrorHandler("cmdLayout_click_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.sGABScript.Declare(String)

Gui.FrmInfo..Visible(False)

F.Intrinsic.String.Build("{0}\GAB\GAS\GAB_Fin_State_Layouts.gas",V.Caller.PluginsDir,V.Local.sGABScript)
F.Global.General.CallSyncGAS(V.Local.sGABScript)

Gui.FrmInfo..Visible(True)

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("cmdLayout_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Layout.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.cmdLayout_click.End

Program.Sub.subCreateTable.Start
'Create tables if missing

F.Intrinsic.Control.SetErrorHandler("subCreateTable_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)
'============================================================================================================================================================================================================================

V.Local.bExists.Declare(Boolean)

'GAB_FIN_STATE_INFO
F.ODBC.Connection!con.TableExists("GAB_FIN_STATE_INFO",V.Local.bExists)
F.Intrinsic.Control.If(V.Local.bExists,=,False)
	F.ODBC.Connection!con.Execute("CREATE TABLE GAB_FIN_STATE_INFO(ID IDENTITY DEFAULT '0',STATEMENT_NUM INTEGER,GROUP_ORDER INTEGER,SUB_GROUP INTEGER,TITLE VARCHAR(50),BEG_ACCOUNT VARCHAR(12),END_ACCOUNT VARCHAR(12),FLAG_TOTAL VARCHAR (1),PRIMARY KEY (ID),UNIQUE (ID));")
F.Intrinsic.Control.EndIf

'============================================================================================================================================================================================================================
F.Intrinsic.Control.Label("subCreateTable_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GAB_Fin_State_Layout.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf

Program.Sub.subCreateTable.End


